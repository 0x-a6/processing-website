---
name: 'pdf'
title: 'Exportar PDF '
library: 'true'
description: 'Crea archivos PDF. Estos archivos con gráficos en vector pueden ser escalados a cualquier tamaño e impresos en alta resolución.'
---

## Exportar PDF

La biblioteca PDF hace posible escribir archivos PDF directamente desde Processing. Estos archivos con gráficos en vector pueden ser escalados a cualquier tamaño e impresos en alta resolución. La biblioteca PDF puede mandar datos 3D a un archivo vectorial 2D, pero para exportar datos 3D, use la biblioteca DXF. El código fuente está disponible en el repositorio Github de [Processing](https://github.com/processing/processing/tree/master/java/libraries/svg). Por favor reporte los errores [aquí](https://github.com/processing/processing/issues).
La biblioteca es frecuentemente usada con la función size() del core de Processing, con una combinación de beginRecord() y endRecord(), o con beginRaw() y endRaw(). La función createGraphics() también puede ser útil. Mira los ejemplos de abajo para ver diferentes técnicas.

## Un Cuadro (Sin Pantalla)

Este ejemplo dibuja un solo cuadro en un archivo PDF y sale. (Nota que no se abrirá ninguna pantalla de despliegue; esto ayuda cuando estas tratando de crear imágenes masivas de PDF que son más grandes que el tamaño de pantalla.)

```
import processing.pdf.*;

void setup() {
  size(400, 400, PDF, "filename.pdf");
}

void draw() {
  // Dibuja algo
  line(0, 0, width/2, height);

  // Sal del programa
  println("Finished.");
  exit();
}
```


## Multiples Páginas (Sin Pantalla)

Es posible escribir cada cuadro como una nueva página en un documento PDF. Este ejemplo crea un documento de 100 páginas:

```
import processing.pdf.*;

void setup() {
  size(400, 400, PDF, "filename.pdf");
}

void draw() {
  // Dibuja algo bueno
  line(0, 0, frameCount * 4, height);

  PGraphicsPDF pdf = (PGraphicsPDF) g;  // Get the renderer

  // Cuando termines, guarda el documento y sal
  if (frameCount == 100) {
    exit();
  } else {
    pdf.nextPage();  // Dile que avance a la siguiente página
  }
}
```

## Un Cuadro (Con Pantalla)

Para dibujar a pantalla mientras guardas un PDF, usa las funciones beginRecord() y endRecord(). Esto es más lento, pero es útil cuando quieres ver en que estas trabajando mientras se graba.

```
import processing.pdf.*;

void setup() {
  size(400, 400);
  noLoop();
  beginRecord(PDF, "filename.pdf");
}

void draw() {
  // Dibuja algo bonito
  line(0, 0, width/2, height);

  endRecord();
}
```

## Un Cuadro de una Animación (Con Pantalla)

También es posible guardar un cuadro del programa con elementos móviles. Crea una variable booleana para encender y apagar el proceso de guardar el PDF

```
import processing.pdf.*;

boolean record;

void setup() {
  size(400, 400);
}

void draw() {
  if (record) {
    // Nota que #### será reemplazado con el número de cuadro. ¡Elegante!
    beginRecord(PDF, "frame-####.pdf");
  }

  // Dibuja algo bueno aquí
  background(255);
  line(mouseX, mouseY, width/2, height/2);

  if (record) {
    endRecord();
  record = false;
  }
}

// Presiona una tecla para que miles de archivos no sean creadosvoid mousePressed() {
  record = true;
}
```

## Multiples Cuadros en Un Archivo (Con Pantalla)

El siguiente ejemplo guarda absolutamente todo lo que pasa mientras el sketch se esta ejecutando. Teclear la tecla 'q' quitará el sketch. El sketch llama al método exit(), lo cual es necesario para asegurarse de que el archivo es escrito apropiadamente cuando se complete la operación.

```
import processing.pdf.*;

void setup() {
  size(400, 400);
  beginRecord(PDF, "everything.pdf");
}

void draw() {
  // Asegurate de no llamar al background, de otra manera
  // se acumulará muchos datos que harán tu cuadro invisible

  // Dibuja algo bonito aquí
  line(mouseX, mouseY, width/2, height/2);
}

void keyPressed() {
  if (key == 'q') {
    endRecord();
    exit();
  }
}
```

## Pausar mientras se Graba (Con Pantalla)

It's also possible to pause/resume recording along the way. The following example toggles recording on/off each time you hit the 'r' key. Pressing the 'q' key will quit the sketch.

```
import processing.pdf.*;

boolean recording;
PGraphicsPDF pdf;

void setup() {
  size(400, 400);
  pdf = (PGraphicsPDF) createGraphics(width, height, PDF, "pause-resume.pdf");
}

void draw() {
// Asegurate de no llamar al background, de otra manera
  // se acumulará muchos datos que harán tu cuadro invisible

  // Dibuja algo bonito aquí
  if (mousePressed) {
    line(pmouseX, pmouseY, mouseX, mouseY);
  }
}

void keyPressed() {
  if (key == 'r') {
    if (recording) {
      endRecord();
      println("Recording stopped.");
      recording = false;
    } else {
      beginRecord(pdf);
      println("Recording started.");
      recording = true;
    }
  } else if (key == 'q') {
    if (recording) {
      endRecord();
    }
    exit();
  }
}
```

## Archivos PDF desde una Geometría 3D (Con Pantalla)

Para crear vectores desde datos 3D, usa los métodos beginRaw() y endRaw(). Estos métodos obtendrán los datos de la figura justo antes de ser mostrados en pantalla. A esta altura, la escena completa no es nada más que una larga lista de líneas y triángulos. Esto significa que una figura creada con el método sphere() estará hecha de cientos de triángulos, y no de un sólo objeto.

Cuando usas beginRaw() y endRaw(), es posible escribir tanto al renderer 2D como al 3D. Por ejemplo, beginRaw() con la biblioteca PDF escribirá la geometría como triángulos y líneas aplanadas.

```
import processing.pdf.*;

boolean record;

void setup() {
  size(500, 500, P3D);
}

void draw() {
  if (record) {
    beginRaw(PDF, "output.pdf");
  }

  // Dibuja aquí
  background(204);
  translate(width/2, height/2, -200);
  rotateZ(0.2);
  rotateY(mouseX/500.0);
  box(200);

  if (record) {
    endRaw();
    record = false;
  }
}

// Teclea 'r' para guardar un solo cuadro
void keyPressed() {
  if (key == 'r') {
    record = true;
  }
}
```

## Usar createGraphics() para Crear un archivo PDF

Para escribir un archivo PDF usando solamente el comando createGraphics(), en vez de como parte de un sketch, es necesario llamar dispose() en el objeto PGraphicsPDF. Esto es lo mismo que llamar exit(), pero sin salir del sketch.

```
import processing.pdf.*;

PGraphics pdf = createGraphics(300, 300, PDF, "output.pdf");
pdf.beginDraw();
pdf.background(128, 0, 0);
pdf.line(50, 50, 250, 250);
pdf.dispose();
pdf.endDraw();
```

## Notas adicionales para el renderer PDF:

Si quieres datos 3D, usa la biblioteca de grabado DXF.

- Usar hint(ENABLE_DEPTH_SORT) puede mejorar la apariencia de la geometría 3D dibujada en archivos con formato 2D.
- Las imágenes no lucen muy bien, debido a la diferencia de expectativas en como un PDF debe lucir (escalable y de alta resolución) contra lo que pasa con los datos de la imagen, que es escrita a una resolución de 72 dpi.
- A partir del lanzamiento 0120, el texto no es tratado como datos de figura por default, lo que significa que la fuente tendrá que ser instalada para ver el PDF creado. La parte positiva de esto es que el PDF se desplegara mejor. - Para forzar que el texto sea tratado como datos de la figura, usa textMode(SHAPE) inmediatamente después de size().
- Otra opción para tipo es usar createFont() con una fuente TrueType (algunas fuentes OpenType también funcionan). Cualquier fuente que se muestre en PFont.list() debería funcionar, y si no, agregar un archivo .ttf al directorio data y llamar createFont("fontname.ttf") también funcionará.
- Si loadFont() es usado en vez de createFont(), el texto pudiera aparecer pixelado y feo. En algunos casos hint(ENABLE_NATIVE_FONTS) puede mejorar la situación si la fuente está instalada en la máquina.
- Para iniciar una nueva página hay que llamar a nextPage() así:
  ```
  PGraphicsPDF pdf = (PGraphicsPDF) g;
  pdf.nextPage();
  ```
  (Este ejemplo también es mostrado arriba)

- De nuevo, llamar a exit() es realmente importante cuando usas archivos PDF con size().
