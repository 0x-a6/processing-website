---
name: 'pdf'
title: 'Exportar PDF '
library: 'true'
description: 'Crea archivos PDF. Estos archivos con gráficos en vector pueden ser escalados a cualquier tamaño e impresos en alta resolución.'
---

## Exportar PDF

La biblioteca PDF hace posible escribir archivos PDF directamente desde Processing. Estos archivos con gráficos en vector pueden ser escalados a cualquier tamaño e impresos en alta resolución. La biblioteca PDF puede mandar datos 3D a un archivo vectorial 2D, pero para exportar datos 3D, use la biblioteca DXF. El código fuente está disponible en el repositorio Github de [Processing](https://github.com/processing/processing/tree/master/java/libraries/svg). Por favor reporte los errores [aquí](https://github.com/processing/processing/issues).
La biblioteca es frecuentemente usada con la función size() del core de Processing, con una combinación de beginRecord() y endRecord(), o con beginRaw() y endRaw(). La función createGraphics() también puede ser útil. Mira los ejemplos de abajo para ver diferentes técnicas.

## Un Cuadro (Sin Pantalla)

Este ejemplo dibuja un solo cuadro en un archivo PDF y sale. (Nota que no se abrirá ninguna pantalla de despliegue; esto ayuda cuando estas tratando de crear imágenes masivas de PDF que son más grandes que el tamaño de pantalla.)

```
import processing.pdf.*;

void setup() {
  size(400, 400, PDF, "filename.pdf");
}

void draw() {
  // Dibuja algo
  line(0, 0, width/2, height);

  // Sal del programa
  println("Finished.");
  exit();
}
```


## Multiples Páginas (Sin Pantalla)

Es posible escribir cada cuadro como una nueva página en un documento PDF. Este ejemplo crea un documento de 100 páginas:

```
import processing.pdf.*;

void setup() {
  size(400, 400, PDF, "filename.pdf");
}

void draw() {
  // Dibuja algo bueno
  line(0, 0, frameCount * 4, height);

  PGraphicsPDF pdf = (PGraphicsPDF) g;  // Get the renderer

  // Cuando termines, guarda el documento y sal
  if (frameCount == 100) {
    exit();
  } else {
    pdf.nextPage();  // Dile que avance a la siguiente página
  }
}
```

## Un Cuadro (Con Pantalla)

Para dibujar a pantalla mientras guardas un PDF, usa las funciones beginRecord() y endRecord(). Esto es más lento, pero es útil cuando quieres ver en que estas trabajando mientras se graba.

```
import processing.pdf.*;

void setup() {
  size(400, 400);
  noLoop();
  beginRecord(PDF, "filename.pdf");
}

void draw() {
  // Dibuja algo bonito
  line(0, 0, width/2, height);

  endRecord();
}
```

## Un Cuadro de una Animación (Con Pantalla)

También es posible guardar un cuadro del programa con elementos móviles. Crea una variable booleana para encender y apagar el proceso de guardar el PDF

```
import processing.pdf.*;

boolean record;

void setup() {
  size(400, 400);
}

void draw() {
  if (record) {
    // Nota que #### será reemplazado con el número de cuadro. ¡Elegante!
    beginRecord(PDF, "frame-####.pdf");
  }

  // Dibuja algo bueno aquí
  background(255);
  line(mouseX, mouseY, width/2, height/2);

  if (record) {
    endRecord();
  record = false;
  }
}

// Presiona una tecla para que miles de archivos no sean creadosvoid mousePressed() {
  record = true;
}
```

## Multiples Cuadros en Un Archivo (Con Pantalla)

El siguiente ejemplo guarda absolutamente todo lo que pasa mientras el sketch se esta ejecutando. Teclear la tecla 'q' quitará el sketch. El sketch llama al método exit(), lo cual es necesario para asegurarse de que el archivo es escrito apropiadamente cuando se complete la operación.

```
import processing.pdf.*;

void setup() {
  size(400, 400);
  beginRecord(PDF, "everything.pdf");
}

void draw() {
  // Asegurate de no llamar al background, de otra manera
  // se acumulará muchos datos que harán tu cuadro invisible

  // Dibuja algo bonito aquí
  line(mouseX, mouseY, width/2, height/2);
}

void keyPressed() {
  if (key == 'q') {
    endRecord();
    exit();
  }
}
```

## Pausar mientras se Graba (Con Pantalla)

It's also possible to pause/resume recording along the way. The following example toggles recording on/off each time you hit the 'r' key. Pressing the 'q' key will quit the sketch.

```
import processing.pdf.*;

boolean recording;
PGraphicsPDF pdf;

void setup() {
  size(400, 400);
  pdf = (PGraphicsPDF) createGraphics(width, height, PDF, "pause-resume.pdf");
}

void draw() {
// Asegurate de no llamar al background, de otra manera
  // se acumulará muchos datos que harán tu cuadro invisible

  // Dibuja algo bonito aquí
  if (mousePressed) {
    line(pmouseX, pmouseY, mouseX, mouseY);
  }
}

void keyPressed() {
  if (key == 'r') {
    if (recording) {
      endRecord();
      println("Recording stopped.");
      recording = false;
    } else {
      beginRecord(pdf);
      println("Recording started.");
      recording = true;
    }
  } else if (key == 'q') {
    if (recording) {
      endRecord();
    }
    exit();
  }
}
```

## Archivos PDF desde una Geometría 3D (Con Pantalla)

To create vectors from 3D data, use the beginRaw() and endRaw() commands. These commands will grab the shape data just before it is rendered to the screen. At this stage, your entire scene is nothing but a long list of lines and triangles. This means that a shape created with sphere() method will be made up of hundreds of triangles, rather than a single object.

When using beginRaw() and endRaw(), it's possible to write to either a 2D or 3D renderer. For instance, beginRaw() with the PDF library will write the geometry as flattened triangles and lines.

```
import processing.pdf.*;

boolean record;

void setup() {
  size(500, 500, P3D);
}

void draw() {
  if (record) {
    beginRaw(PDF, "output.pdf");
  }

  // Dibuja aquí
  background(204);
  translate(width/2, height/2, -200);
  rotateZ(0.2);
  rotateY(mouseX/500.0);
  box(200);

  if (record) {
    endRaw();
    record = false;
  }
}

// Hit 'r' to record a single frame
void keyPressed() {
  if (key == 'r') {
    record = true;
  }
}
```

## Usar createGraphics() para Crear un archivo PDF

To write a PDF file using only the createGraphics() command, rather than as part of a sketch, it's necessary to call dispose() on the PGraphicsPDF object. This is the same as calling exit(), but it won't quit the sketch.

```
import processing.pdf.*;

PGraphics pdf = createGraphics(300, 300, PDF, "output.pdf");
pdf.beginDraw();
pdf.background(128, 0, 0);
pdf.line(50, 50, 250, 250);
pdf.dispose();
pdf.endDraw();
```

## Notas adicionales para el renderer PDF:

If you want 3D data, use the DXF recording library instead.

- Using hint(ENABLE_DEPTH_SORT) can improve the appearance of 3D geometry drawn to 2D file formats.
- Images don't look great, mostly because of the difference of expectations in how a PDF should look (scalable and high res) versus what happens when image data is written to it at 72 dpi.
- Starting in release 0120, text is no longer treated as shape data by default, meaning that the font will need to be installed to view the PDF that's created. The upside is that the PDF will render better. - To force text to be treated as shape data, use textMode(SHAPE), immediately after size().
- Another option for type, is to use createFont() with a TrueType font (some OpenType fonts also work). Any font that shows up in PFont.list() should work, or if not, adding a .ttf file to the data directory and calling createFont("fontname.ttf") will also work.
- If loadFont() is used instead of createFont(), the text may show up bitmapped and ugly. In some cases, hint(ENABLE_NATIVE_FONTS) may improve the situation if the font is installed on the machine.
- Starting a new page is a matter of calling nextPage() like so:
  ```
  PGraphicsPDF pdf = (PGraphicsPDF) g;
  pdf.nextPage();
  ```
  (This example is also shown above)

- Again, exit() is really important when using PDF with size().
